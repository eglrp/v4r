enable_testing()
add_custom_target(tests "${CMAKE_CTEST_COMMAND}" "-V" VERBATIM)

get_filename_component(TEST_DATA_DIR "data" ABSOLUTE)

foreach(_module ${V4R_MODULES})
  if(HAVE_${_module})
    set(_test_dir "${V4R_MODULE_${_module}_LOCATION}/test")
    if(EXISTS ${_test_dir} AND IS_DIRECTORY ${_test_dir})
      file(GLOB _test_files "${_test_dir}/*.cpp")
      foreach(_f ${_test_files})
        get_filename_component(_name ${_f} NAME_WE)
        add_executable(${_name} ${_f})
        add_test(NAME "${_module}_${_name}" COMMAND ${_name})
        add_dependencies(tests ${_name})
        target_link_libraries(${_name} ${_module} gtest ${Boost_FILESYSTEM_LIBRARY} ${Boost_SYSTEM_LIBRARY})
        target_include_directories(${_name} PRIVATE ${CMAKE_CURRENT_SOURCE_DIR})
        target_compile_definitions(${_name} PRIVATE "TEST_DATA_DIR=\"${TEST_DATA_DIR}\"")
      endforeach()
    endif()
  endif()
endforeach()
